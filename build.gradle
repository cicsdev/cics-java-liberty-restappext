plugins
{
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'war'
    id 'com.ibm.cics.bundle' version '1.0.7'
}

group = 'com.ibm.cicsdev.restappext'
archivesBaseName='cics-java-liberty-restappext'
version = '0.1.0'


// If in Eclipse, add Javadoc to the local project classpath
eclipse 
{
    classpath 
    {
        downloadJavadoc = true
    }
}


repositories 
{
    mavenCentral()
}


dependencies 
{      
    
    compileOnly enforcedPlatform("com.ibm.cics:com.ibm.cics.ts.bom:5.5-20200519131930-PH25409") // CICS TS V5.5 Maven BOM 

    // Don't include JCICS in the final build (no need for version because we have BOM)
    compileOnly("com.ibm.cics:com.ibm.cics.server")
    compileOnly("com.ibm.cics:com.ibm.cics.server.invocation.annotations")

    // Use the V6 annotation processor as this supports Java 11
    annotationProcessor("com.ibm.cics:com.ibm.cics.server.invocation:6.1")

    // Compile against, but don't include Java EE components in the final build 
    compileOnly ("javax.ws.rs:javax.ws.rs-api:2.0")
    compileOnly ("javax.servlet:javax.servlet-api:3.1.0")        
    //JAXB required if compiling with Java 11 or later
    compileOnly ("javax.xml.bind:jaxb-api:2.3.1")

    // Add generated IRG JAR as dependency to be packaged with app
    implementation files('lib/cics-java-liberty-restappext-generated.jar')

    // Add the files from the war task as CICS bundle parts
    cicsBundlePart files(war)
}    

// CICS bundle build, specify defaultJVMServer value to match CICS JVMSERVER name
cicsBundle
{
    build
    {
        defaultJVMServer = project.findProperty("cics.jvmserver") ?: "DFHWLP"
    }
}